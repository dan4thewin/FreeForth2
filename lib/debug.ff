\ Alternate compiler that makes verbose words
\ debug` ( -- ) swaps normal compiler for debug compiler
\ nodebug` ( -- ) swaps compiler back to normal
" debug" features append
needs see.ff ;

: _append ( @ # c@ -- ) 2dup c@ + over 2>r c@+ + place drop 2r>
  2dup c! + 1+ 0 swap c! ;
\ source copied here during compilation
tib $2'0000+ constant sob pvt 0 sob!	\ start-of-blocks
variable bbuf pvt sob bbuf!		\ block buffer
variable prev pvt sob prev!
:. tow		prev@ c@+ + bbuf@ >= drop IF drop ;THEN prev! ;
:. bba		tow tuck bbuf@ 0 overc! _append bbuf@ + 1+ bbuf! ;
:. concat	prev@ dupc@ tuck over+ 1+ dupc@ swap $20 swap c! rot + 1+ swap c! ;

\ use custom reset and call, for a fixed width that -call. can rewind
:. xsa		SC@ 1& BEGIN 0 CASE $90, BREAK 1 CASE $94, BREAK END ,1 ;	\ 94(xchg esp,eax)
:. call         $e8, ,1 here 4+ - , ;
:. pushf	$9c, ,1 ;
:. popf		$9d, ,1 ;

\ pass counted-string @ to observer with esi
:. @>si		$be, ,1 bbuf@ , ;
\ obs/xobsx calls macros si>b and xbd since it executes at runtime
:. si>b`	dup` $f389, ,2 ;
:. xbd`		$da87, ,2 ;
: obs		24 atx ss si>b c@+ magenta foreground type normal ;
: xobsx		xbd obs xbd ;
:. sc?obs	SC@ 2& 0= drop IF obs ' ELSE xobsx ' THEN ;
:. obs,		xsa pushf sc?obs call popf xsa ;

variable _here pvt here _here!
-14 constant ol pvt
:. rewind	ol allot here _here! concat ;

:. ct		swap 7& 2* + BEGIN 0 CASE execute BREAK 1 CASE call, BREAK
		2 CASE ( leave on stack ) BREAK 3 CASE lit` BREAK !"bad type" END ;
:. xtct		find 0- 0= IF which@ h.ct+ c@ swap THEN ;
:. endc!	>r 2dup + r> swap c! ;
:. dbg,		here ol+ _here@ = 2drop IF rewind ELSE here _here! THEN
		@>si obs, 2dup bba ;
:. dbgc		BEGIN wsparse 0- 0> WHILE  dbg,
		'`'  endc! 1+ xtct 0- 0= IF    ct AGAIN
		1- 0 endc!    xtct 0- 0= IF 1_ ct AGAIN
		litcomp REPEAT 2drop ;

: ui.		24 atx ss prompt ;
: >mov.		rewind >mov x^ ;
: -call.	rewind -call x^ ;
: ;.`		tailrec@ 0- drop 0<> IF here ol+ callmark@ = 2drop IF rewind THEN THEN ;` x^ ;
: nodebug`	ui ^^ >mov ^^ -call ^^ ;` ^^ compiler ^^ ;
: debug`	ui. ' ui !^ >mov. ' >mov !^ -call. ' -call !^ ;.` ' ;` !^ dbgc ' compiler !^ ;
hidepvt ;
